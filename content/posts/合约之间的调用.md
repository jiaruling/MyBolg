---
title: "合约之间的调用"
date: 2024-05-10T16:07:35+08:00
lastmod: 2024-05-10T16:07:35+08:00
author: ["me"]
categories: ["web3"] # 没有分类界面可以不填写
tags: [""] # 标签
description: ""
weight:
slug: ""
draft: false # 是否为草稿
comments: true # 本页面是否显示评论
reward: true # 打赏
mermaid: true #是否开启mermaid
showToc: true # 显示目录
TocOpen: true # 自动展开目录
hidemeta: false # 是否隐藏文章的元信息，如发布日期、作者等
disableShare: true # 底部不显示分享栏
showbreadcrumbs: true #顶部显示路径
#   cover:
#     image: "" #图片路径例如：posts/tech/123/123.png
#     zoom: # 图片大小，例如填写 50% 表示原图像的一半大小
#     caption: "" #图片底部描述
#     alt: ""
#     relative: false
---

## 原理

> EOA(External owned account), 外部账号，例如 metamask。调用最终是由EOA发起的

![](/image/contractCall.png)

### 调用过程

- 调用者必须持有被调用合约的地址
- 得到被调用合约的信息
- 将地址重载为被调用合约，调用它的函数

## 静态调用

#### 在源文件内部

- 调用者直接将被调用合约的地址重载为合约实例
- 用合约实例调用合约函数

在源文件`FunctionCallSameFile.sol`中输入以下代码:

```solidity

// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

// 先部署,得到Callee的合约地址
contract Callee {
    uint public x;
    function setX(uint _x) public {
        x = _x;
    }
}

// 后部署，部署时要初始化Callee的合约地址
contract Caller {
    address calleeAddress;
    
    constructor(address _calleeAddress) {
        calleeAddress = _calleeAddress;
    }

    function setCalleeX(uint _x) public {
        Callee callee = Callee(calleeAddress); // 将合约地址重载为合约实例
        callee.setX(_x); // 通过合约实例调用合约函数
    }
}
```

#### 在源文件之间

- import导入被调用合约的源文件
- 调用者将被调用合约的地址重载为合约实例
- 用合约实例调用合约函数

先部署源文件`Callee.sol`

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract Callee {
    uint public x;
    function setX(uint _x) public {
        x = _x;
    }
}
```

再部署源文件`Caller.sol`

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

import "contracts/Callee.sol"; // 导入被调用的合约

contract Caller {
    address calleeAddress;
    
    constructor(address _calleeAddress) {
        calleeAddress = _calleeAddress;
    }

    function setCalleeX(uint _x) public {
        Callee callee = Callee(calleeAddress);  // 将合约地址重载为合约实例
        callee.setX(_x); // 通过合约实例调用合约函数
    }
}
```

### 接口调用

> 接口调用，只需要关注被调用方法的名称、参数和返回值，不需要关注方法的具体实现。合约接口等价于ABI(应用二进制接口)

#### 接口调用的案例

- 定义接口文件`ICallee.sol`

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  // 使用interface定义一个接口
  interface ICallee { 
      function setX(uint _x) external; // 定义接口要实现的方法，可见性必须为external
  }
  ```

- 定义接口的具体实现`Callee.sol`

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  // 导入需要实现的合约接口
  import "contracts/ICallee.sol";
  
  contract Callee is ICallee  { // 继承需要实现的接口
      uint public x;
      function setX(uint _x) public {
          x = _x;
      }
  }
  ```

- 定义调用合约的合约`Caller.sol`

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  import "contracts/ICallee.sol"; // 导入需要调用的合约接口
  
  contract Caller {
      address calleeAddress;
      
      constructor(address _calleeAddress) {
          calleeAddress = _calleeAddress;
      }
  
      function setCalleeX(uint _x) public {
          ICallee icallee = ICallee(calleeAddress); // 使用接口实现合约的实例化
          icallee.setX(_x);
      }
  }
  ```

#### 生动的例子

- USDT: 符合ERC20标准的美元稳定币，tether合约

- 获得测试网上tether合约地址: `0xdac17f958d2ee523a2206206994597c13d831ec7`

- 通过自己写的ERC20接口访问这个合约

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  // 定义ERC20接口
  interface IERC20 {
      function name() external view returns(string memory);
      function symbol() external view returns(string memory);
      function decimals() external view returns(uint8);
  }
  ```

  注释: 接口也可以直接编译和部署。编译和普通合约没有区别，部署的时候，需要提供已有合约实例的地址，如下图所示:

  ![](/image/interface.png)

### ABI探秘

1. EOA使用的ABI数据, 与合约间调用时调用者持有的接口是等价的，都是对合约函数签名的完整描述

2. 调用者使用ABI数据生成message中的calldata

   ![](/image/ABI.png)

#### ABI与合约接口之间的联系

1. 在Remix的`contracts`目录下新建一个solidity源文件，文件名为`Iinterface.sol`, 并输入以下代码

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

// 定义ERC20接口
interface Iinerface {
}
```

2. 输入代码后保存文件，Remix工具会自动的在`contracts/artifacts`目录下生成与`Iinterface.sol`文件同名的两个json文件，分别是`Iinterface.json`与`Iinterface_metadata.json`。重点是`Iinterface_metadata.json`文件。

3. 此时生成的`Iinterface_metadata.json`的文件内容如下

   ```solidity
   {
   	"compiler": {
   		"version": "0.8.25+commit.b61c2a91"
   	},
   	"language": "Solidity",
   	"output": {
   		"abi": [],
   		"devdoc": {
   			"kind": "dev",
   			"methods": {},
   			"version": 1
   		},
   		"userdoc": {
   			"kind": "user",
   			"methods": {},
   			"version": 1
   		}
   	},
   	"settings": {
   		"compilationTarget": {
   			"contracts/Iinterface.sol": "Iinerface"
   		},
   		"evmVersion": "cancun",
   		"libraries": {},
   		"metadata": {
   			"bytecodeHash": "ipfs"
   		},
   		"optimizer": {
   			"enabled": false,
   			"runs": 200
   		},
   		"remappings": []
   	},
   	"sources": {
   		"contracts/Iinterface.sol": {
   			"keccak256": "0x5433d311b824411483d9eafe64b678eb84750fd3534f74d19992cdc6d50add56",
   			"license": "GPL-3.0",
   			"urls": [
   				"bzz-raw://e0df3fe85e33a1319609f92d9d358c1fc512e49764da3b37f903383cc8980bc4",
   				"dweb:/ipfs/QmZQK7s7KADWxd96EfN6xjoFUDZNMdFkTLQBYQyGCxQY58"
   			]
   		}
   	},
   	"version": 1
   }
   ```

   在第7行可见`abi`数据为空数组

4. 在`Iinterface.sol`文件中新增一个合约函数签名，并保存文件

   ```solidity
   // SPDX-License-Identifier: GPL-3.0
   
   pragma solidity >=0.8.2 <0.9.0;
   
   // 定义ERC20接口
   interface Iinerface {
       function setX(uint _x) external returns(uint);
   }
   ```

5. 新生成的`Iinterface_metadata.json`的文件内容如下

   ```solidity
   {
   	"compiler": {
   		"version": "0.8.25+commit.b61c2a91"
   	},
   	"language": "Solidity",
   	"output": {
   		"abi": [
   			{
   				"inputs": [
   					{
   						"internalType": "uint256",
   						"name": "_x",
   						"type": "uint256"
   					}
   				],
   				"name": "setX",
   				"outputs": [
   					{
   						"internalType": "uint256",
   						"name": "",
   						"type": "uint256"
   					}
   				],
   				"stateMutability": "nonpayable",
   				"type": "function"
   			}
   		],
   		"devdoc": {
   			"kind": "dev",
   			"methods": {},
   			"version": 1
   		},
   		"userdoc": {
   			"kind": "user",
   			"methods": {},
   			"version": 1
   		}
   	},
   	"settings": {
   		"compilationTarget": {
   			"contracts/Iinterface.sol": "Iinerface"
   		},
   		"evmVersion": "cancun",
   		"libraries": {},
   		"metadata": {
   			"bytecodeHash": "ipfs"
   		},
   		"optimizer": {
   			"enabled": false,
   			"runs": 200
   		},
   		"remappings": []
   	},
   	"sources": {
   		"contracts/Iinterface.sol": {
   			"keccak256": "0xa8cd0ed9797f6a71b865dbce332b4e4df95a35b339baf5de37f5b25ce9d9a323",
   			"license": "GPL-3.0",
   			"urls": [
   				"bzz-raw://224ebe4385d1f9679a0163e0902b57971dfeebf988297a85271718bb707ecd23",
   				"dweb:/ipfs/QmatYLDmmwDbFKehKCVYkK3r1W5gV4A911Fb9FRehXewEC"
   			]
   		}
   	},
   	"version": 1
   }
   ```

   在第7行到27行，可见`abi`有新生成的数据。abi中所描述的数据和接口文件中的函数定义保持一致。

### 合约调用中的上下文变化

#### Transaction和block

任何合约函数调用最终是由一个EOA发送一个transaction触发的，并通过合约之间的进一步调用形成一个调用链。无论这个链条有多长，任何环节的函数都能看到、访问这个链条的发起者transaction，它是一个完整调用链条的**全局性上下文变量**，变量名称为tx。transaction总是来自于一个区块，这个区块对应的上下文变量名字是block。

![](/image/tx.png)

#### message上下文

1. 合约之间的调用也模拟transaction的结构，并且叫message，变量名msg
2. EOA调用的message可以认为是transaction的一个拷贝
3. Message在合约间调用时新产生。调用链条中，合约内部调用看到的是一个新的messsage，不同合约看到不同的message

![](/image/message.png)

#### 编码实战

- 合约`Callee.sol`

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  import "contracts/ICallee.sol";
  
  contract Callee is ICallee  {
      uint public x;
      address public sender; // 记录message上下文
      address public EOASender; // 记录tx上下文
      function setX(uint _x) public {
          sender = msg.sender;
          EOASender = tx.origin;
          x = _x;
      }
  }
  ```

- 合约`Caller.sol`

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  import "contracts/ICallee.sol"; // 导入被调用的合约
  
  contract Caller {
      address calleeAddress;
      address public sender; // 记录message上下文
      address public EOASender; // 记录tx上下文
      
      constructor(address _calleeAddress) {
          calleeAddress = _calleeAddress;
      }
  
      function setCalleeX(uint _x) public {
          sender = msg.sender;
          EOASender = tx.origin;
          ICallee callee = ICallee(calleeAddress);
          callee.setX(_x);
      }
  }

- 先部署`Callee.sol`合约，再部署`Caller.sol`合约。
- 在`Caller.sol`合约实例中执行`SetCalleeX`函数，观察两个合约实例中的`EOASender`和`sender`值的变化。
  - `Caller.sol`的合约实例: `EOASender=sender=EOA`
  - `Callee.sol`的合约实例: `EOASender=EOA` 、`sender=Caller.sol的合约实例地址`

### External关键字

> external修饰的函数**应该**只被外部函数调用。(但也可以被内部调用，但是这种内部调用也是有外部调用机制，即新产生message!)

#### 编码实验

- 实现下面合约并观察运行结果：**public关键字**

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.7.0 <0.9.0;
  
  contract ExternalDemo {
      address public caller;
  
      function first() public {
          second();
      }
  
      function second() public {
          caller = msg.sender;
      }
  }
  
  // 运行结果
  // EOA账号: 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4
  // 合约实例地址: 0x7EF2e0048f5bAeDe046f6BF797943daF4ED8CB47
  // caller: 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4
  ```

  注: `EOA=caller`，说明是同一个message上下文

- 实现下面合约并观察运行结果：**public关键字 + this**

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.7.0 <0.9.0;
  
  contract ExternalDemo {
      address public caller;
  
      function first() public {
          this.second(); // 重点: 使用this关键字进行调用
      }
  
      function second() public {
          caller = msg.sender;
      }
  }
  
  // 运行结果
  // EOA账号: 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4
  // 合约实例地址: 0x358AA13c52544ECCEF6B0ADD0f801012ADAD5eE3
  // caller: 0x358AA13c52544ECCEF6B0ADD0f801012ADAD5eE3
  ```

  注: `合约实例地址=caller`，说明产生了新的message上下文

- 实现下面合约并观察运行结果：**external关键字**

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.7.0 <0.9.0;
  
  contract ExternalDemo {
      address public caller;
  
      function first() public {
          this.second(); // 调用external修饰的函数，必须使用this关键字进行调用，否则编译器会报错
      }
  
      function second() external  { // 使用external关键字
          caller = msg.sender;
      }
  }
  
  // 运行结果
  // EOA账号: 0x5B38Da6a701c568545dCfcB03FcB875f56beddC4
  // 合约实例地址: 0xd2a5bC10698FD955D1Fe6cb468a17809A08fd005
  // caller: 0xd2a5bC10698FD955D1Fe6cb468a17809A08fd005
  ```

  注: `合约实例地址=caller`，说明产生了新的message上下文

## 动态调用

### 动态调用的作用

- 类似于其他语言的反射
- 能够开发框架性代码

### 动态调用的语法-call

> 参考: https://solidity-by-example.org/call/

> `(bool success, bytes data) = <address>.call(byte calldata)`

- call是address的方法
- call返回值`(bool success, bytes data)`
- 忽略返回值`success`， 会造成严重问题
- 动态调用进一步降低了代码的耦合性

#### 实战编码

- 先编写被调用的合约: Callee.sol

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  contract Callee {
      uint public x;
      function setX(uint256 _x) public returns(uint) {
          x = _x;
          return x;
      }
  }
  ```

- 再编写发起调用的合约: Caller.sol

  ```solidity
  // SPDX-License-Identifier: GPL-3.0
  
  pragma solidity >=0.8.2 <0.9.0;
  
  contract Caller {
      address calleeAddress;
      uint public  xx;
  
      constructor(address _calleeAddress) {
          calleeAddress = _calleeAddress;
      }
  
      function setCalleeX(uint _x) public {
          bytes memory cd = abi.encodeWithSignature("setX(uint256)", _x); // 编码: 构造calldata参数，注意: 不能使用uint256的别名uint
          (bool suc, bytes memory rest) = calleeAddress.call(cd); //发起动态调用
          if (!suc) {
              revert("call failed"); // 结束程序的执行，并返回报错的内容
          }
          (uint x) = abi.decode(rest, (uint)); // 解码: 将二进制数据转换为对于类型的数据
          xx = x;
      }
  }
  
  ```

  先部署`Callee.sol`，再部署`Caller.sol`。调用`setCalleeX`函数，查看两个合约的状态变换。

### fallback函数

> 参考: https://solidity-by-example.org/fallback/

- 特殊函数，**“备胎”函数**
- 动态调用call绕过类型检查，或者自己定义一个接口函数，但是自定义的函数可能不存在，fallback函数才有机会起作用。
- proxy模式中有重要应用: delegatecall: 支持合约升级
- 转账功能中有重要作用

#### 编码实战

- 先编写被调用的合约: Callee.sol

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract Callee {
    uint public x;
    function setX(uint256 _x) public returns(uint) {
        x = _x;
        return x;
    }

    fallback() external { // 重点
        x = 10000000;
    }
}
```

- 再编写发起调用的合约: Caller.sol

```solidity
pragma solidity >=0.8.2 <0.9.0;

contract Caller {
    address calleeAddress;
    uint public  xx;

    constructor(address _calleeAddress) {
        calleeAddress = _calleeAddress;
    }

    function setCalleeX(uint _x) public {
        bytes memory cd = abi.encodeWithSignature("setY(uint256)", _x); // 在这里调用了一个Callee合约中不存在的函数
        (bool suc, bytes memory rest) = calleeAddress.call(cd);
        if (!suc) {
            revert("call failed");
        }
        // (uint x) = abi.decode(rest, (uint));
        // xx = x;
    }
}
```

先部署`Callee.sol`，再部署`Caller.sol`。调用`setCalleeX`函数，查看两个合约的状态变换。其中Callee合约中x被设置为10000000,说明当调用Callee合约中不存在的函数时，fallback函数起了作用。

### 动态调用总结

- Call函数是运行时动态生成calldata，并未用到静态ABI数据。但encodeWithSignature中**函数签名字符串**和**参数列表**信息，与ABI(或接口)仍然是等价的。
- Fallback并不是用于处理手误的，而是用于特意安排的应用场景。
