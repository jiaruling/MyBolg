---
title: "数据类型"
date: 2024-05-07T14:53:05+08:00
lastmod: 2024-05-07T14:53:05+08:00
author: ["me"]
categories: ["web3"] # 没有分类界面可以不填写
tags: [""] # 标签
description: ""
weight:
slug: ""
draft: false # 是否为草稿
comments: true # 本页面是否显示评论
reward: true # 打赏
mermaid: true #是否开启mermaid
showToc: true # 显示目录
TocOpen: true # 自动展开目录
hidemeta: false # 是否隐藏文章的元信息，如发布日期、作者等
disableShare: true # 底部不显示分享栏
showbreadcrumbs: true #顶部显示路径
#   cover:
#     image: "" #图片路径例如：posts/tech/123/123.png
#     zoom: # 图片大小，例如填写 50% 表示原图像的一半大小
#     caption: "" #图片底部描述
#     alt: ""
#     relative: false
---

## 值类型(Value)

- 基本数据类型: 整数，枚举，布尔
- Address，contract
- Fixed byte array(定长字节数组)

### 整型

- int/uint以8位字节递增，uint是uint256的别名
- 对于整型数类型x，可以用`type(x).min`和`type(x).max`取这个类型的最小或最大值
- 以太坊虚拟机是256位的机器
- 低版本取模，高版本：异常；Safe'Math

### 枚举

- 枚举是用户创建自定义类型的一种方法(contract, struct, enum)
- 可以与整型数显式转换(隐式不行)
- 整型到枚举的转换会检查是否越界
- 枚举最少一个成员，最多256成员(uint8)，默认值第一个成员
- 从0开始的无符号整型数(与c相同)

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract BasicType{
    function testInt8() public pure returns (uint8) {
        uint8  i8 = 255;
        // i8++; // 运行时数据越界报错
        uint16 i16 = 258;
        // i16 = i8; // uint8给uint16赋值可以
        i8 = uint8(i16); // uint16给uint8赋值必须强制转换 数据超过255会发送丢失，256 -> 0; 257 -> 1; 258 -> 2
        return i8;
    }

    function testInt() public pure returns (uint) {
        uint max = type(uint).max;
        return max;
    }

    // 枚举数据类型
    enum OrderState{layorder, payment}
    function testEnum(uint8 post) public  pure returns (OrderState) {
        OrderState state;
        if (post < 10) {
            state = OrderState.layorder;
        } else {
            state = OrderState.payment;
        }
        return state; // 最终输出的数据类型是uint8
    } 
}
```

### Address

- addres: 20个字节长度
- 关于可支付
  - address payable: 可支付，有成员函数transfer和send
  - address payable可转换为address，反之需要显示转换
- address可转换为uint160和bytes20
- 合约账号地址与外部账号地址 EOA(External Owned Account)
- call，balance等，很大程度上address并不只是一个数据类型

### Contract

- 合约可以隐式转换为它的父合约(多态)
- 合约可以显示转换为地址类型address
- 合约不支持任何运算符
- 从合约变量(实例)调用合约函数(根本性质)
- 合约可用new操作符部署另一合约

### Fixed byte array

- 从bytes1到bytes32（bytes1是8位，bytes32是256位）
- 通过下标访问元素，只能读取不能修改
- 通过length读取长度

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract ComplexValueType{

    // address type
    function testAddress() public view returns(address) {
        address addr = msg.sender; // 获取合约调用者的地址
        return addr;
    }

    function testMyAddress() public view returns(address) {
        address addr = address(this); // 获取本合约的地址
        return addr;
    }

    // contract type
    function testContract() public view returns(ComplexValueType) {
        ComplexValueType myContract = this;
        return myContract;
    }

    // Fixed byte array
     function testFixedByteArray1() public pure returns(bytes3) {
        bytes3 data = 0xf11110; // 默认初始值 0x000000
        return data;            // 0xf11110
    }

    function testFixedByteArray2() public pure returns(bytes1) {
        bytes3 data = 0xf11110;
        bytes1 first = data[0];
        return first;  // 0xf1
    }
}
```

## 引用类型(Reference)

引用类型变量的存储位置(**Location**)，是引用类型特有的，值类型不存在这一概念。

- **EVM**从三个地方访问数据: **Memory**, **Storage**, **Calldata**, 这三个地方就是引用类型变量的存储位置(**Location**)
  - Storage: 持久化的存储，成员变量都是存储在Storage中
  - Memory: EVM的临时存储
  - Calldata: 实际上是**transaction**的数据字段

![](/image/reference.png)

### 数组

- Location不同，完全是两种类型，应各自表述
- storage和memory中的动态数组
- 数组元素的类型限制
- 公有函数的参数和返回值只能是memroy

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract ArrayType {
    // 静态数组
    uint8[3] data;  // 成员变量, 存储在Storage中的数据

    function testStaticArray1() public view returns(uint8[3] memory) { // 公有函数的参数和返回值只能是memroy
        return data; // storage中的数据拷贝到memory中,然后进行返回
    }

    function testStaticArray2() internal  view returns(uint8[3] storage) {
        return data;
    }

    // 动态数组
    uint8[] sdata; // 成员变量, 存储在Storage中的数据

    // storage
    function testReadDynamicStorageArray() public view returns(uint8[] memory) {
        return sdata; // storage中的数据拷贝到memory中,然后进行返回
    }

    function testWriteDynamicStorageArray() public {
        sdata.push(12); // 压栈
        sdata.pop(); // 弹出
        sdata.push(30);
    }

    // memory,在程序运行时动态指定数组的大小，在后续的操作中不能伸缩数组的尺寸
    function testMemoryDynamicArray(uint8 size) public pure returns(uint8[] memory) {
        uint8[] memory mdata = new uint8[](size); //memory当中的动态数组必须做初始化
        //mdata.push(36); // 不存在push操作
        //mdata.pop(38); // 不存在pop操作
        mdata[0] = 1; // 通过下标访问并赋值
        mdata[1] = 2;
        return mdata;
    }
}
```

### 结构

- 结构如同contract和enum，用来自定义数据类型
- 状态变量，局部变量，参数与返回值均可用结构
- 结构可以放在mapping和数组中，结构的成员可以是mapping或数组

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract StructType {
    struct Person {
        string name;
        uint8 age;
        // Home home;
    }

    // struct Home {
    //     string country;
    //     string province;
    //     string city;
    //     // ...
    // }

    Person master; // 成员变量，storage和memory声明自动初始化

    // 测试struct作为返回值
    function readPerson() public view returns (Person memory) {
        return master;
    }
    // 测试struct作为参数
    function writePerson(Person memory p) public {
        master = p;
    }

    function writePersonName(string memory name) public {
        master.name = name;
    }

    // memory
    function testMemoryStruct() public pure returns(Person memory) {
        // 声明自带初始化! 这是递归结构需要考虑的问题...
        Person memory p; // 不用new操作，因为struct大小是确定的，不需要一个尺寸参数！
        //这一点和new动态数组或者bytes，string不同
        p.name = "zhangsan";
        p.age = 25;
        return p;
    }

    // 测试内存location为storage的局部变量
    function testStorageLocalStruct() public view returns(Person memory) {
        Person storage p = master; // 必须指向存在于storage当中的数据块
        // 下面语句修改了master成员变量
        // p.name = "LISI";
        // p.age=18;
        return p;
    }
}
```

### 映射

- 声明形式:`mapping(key-type=>value-type)`
- `key-type`可以是任何基本类型，包括bytes和string，不包括用户自定义的复杂类型--合约、枚举、结构、映射
- `value-type`可以包含mapping在内的任何类型
- 可以作为状态变量、storage型局部变量、库函数的参数；不能作为公有函数的参数和返回值
- public的mapping会自动生成getter；嵌套的mapping生成的mapping会有多个参数
- mapping无法遍历stroage layout
  - 不支持遍历：mapping在storage中的存储方式
  - 不能进行值拷贝: 拷贝是一种搬运过程，搬运总要先把东西找全

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;


contract MappingType {
    // 改为public自动生成getter
    mapping(string => uint8) public ages; // 映射只能放在storage中，不能放在其它location中

    function getAge(string memory name) public view returns(uint8) {
        return ages[name];
    }

    function setAge(string memory name, uint8 age) public {
        ages[name] = age;
    }

    // 一般规则: public memory calldata、internal、private 的返回值可以是storage
    // mapping: 只能是storage
    // public函数的参数或返回值不可能出现mapping类型

    // 嵌套的public映射结构,会有多个查询参数【string,string】
    mapping(string => mapping(string=>uint8)) public ages1;
} 
```

### 引用类型全解析

- 引用类型: 变量本身与变量指向的数据块分离, 赋值操作是引用拷贝，数据块不受影响
- 通常的面向对象语言中的所有引用类型变量之间的赋值操作都是引用拷贝
- 这一点在solidity的引用类型中不成立；solidity的引用类型的变量之间可能发生值拷贝

#### 合约的成员变量的特殊性

与一般虚拟机或者物理机不同，EVM的机器模型中引入了storage，合约的成员变量指向固定的持久化(storage)数据块，它并不像一般引用数据类型变量一样切换他所指向的数据块，由于这个限制，对成员变量的赋值，引用拷贝从技术上变成不可能。

#### Location对数据空间的分割

数据块有三个存储位置(calldata,memory,storage),概念上存储空间被分割成三个子空间，引用类型变量被location属性限定，不可能跨越子空间进行指向切换，只能在子空间内部切换。

#### Calldata的只读属性

1. calldata是message的数据字段
2. 无法向message数据体的calldata数据域中拷贝东西，正如不能修改msg.sender或者msg.value一样

#### 判断算法

> 一个赋值操作: `x=a`

1. 如果x是成员变量，值拷贝，否则:
2. 如果x与a的location相同，引用拷贝，否则: 值拷贝。

#### 检查算法

> 判断算法有两个输出: 引用拷贝，值拷贝。如果输出引用拷贝，不必检查；输出值拷贝，则执行检查算法。

1. 检查x和a的类型中是否有mapping元素(他本身是mapping或者嵌入了mapping成分)，如果有则报错
2. 检查x是否是calldata, 如果是则报错
3. 执行值拷贝

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract LocalStorageVariable {
    uint8[] data1;
    uint8[] data2;

    function getData1() public view returns(uint8[] memory) {
        return data1;
    }

    function getData2() public view returns(uint8[] memory) {
        return data2;
    }

    function inserData1(uint8 d) public {
        data1.push(d);
    }

    function inserData2(uint8 d) public {
        data2.push(d);
    }

    function setDataToData2() public {
        data1 = data2; // 根据判判断算法1: 发生值拷贝
    }

    function testSecondRule(uint8[] calldata pd) public returns(uint8[] memory) {
        uint8[] memory td;
        td = data1; // 根据判判断算法2: 发生值拷贝
        data1 = pd; // 根据判判断算法1: 发生值拷贝
        //pd = data2; // 根据检查算法2: 被赋值变量不能为calldata
        uint8[] calldata cdt = pd; // 根据判判断算法2: 发生引用拷贝
        return td;
    }
}
```

## 字符串和字节数组

> 字符串是不可以修改的，字节数组是可以通过访问下标进行修改的

```solidity
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.8.2 <0.9.0;

contract StringAndBytes {
    // 初始化
    string name = "BytesAndString"; // 或 new string(5)
    bytes name1 = "BytesAndString"; // 或 new bytes(5)

    function testStringAndBytes() public view returns(string memory) {
        string memory data = "xyz"; // 或 new string(5);
        bytes memory data1 = "abc"; // 或 new bytes(5);

        // 不同location的memory拷贝
        data = name;
        data1 = name1;

        // 类型转换
        data1 = bytes(data);
        data = string(data1);

        // 下标访问
        // bytes1 b1 = data1[0];
        data1[0] = 'z';

        // 能够push，pop吗？
        // name1.push(0x00);
        // data1.push(0x00); 局部变量不push，pop

        return data;
    }

    // 高级话题：bytes和byte1[]的区别    
}
```



